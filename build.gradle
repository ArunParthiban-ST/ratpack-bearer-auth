buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
	}
}

def jvmEncoding = java.nio.charset.Charset.defaultCharset().name()
if (jvmEncoding != "UTF-8") {
  throw new IllegalStateException("Build environment must be UTF-8 (it is: $jvmEncoding) - add '-Dfile.encoding=UTF-8' to the GRADLE_OPTS environment variable ")
}

if (!JavaVersion.current().java8Compatible) {
  throw new IllegalStateException("Must be built with Java 8 or higher")
}

task wrapper(type: Wrapper) {
  gradleVersion = "2.7"
}

group = "smartthings.ratpack"
version = rootProject.file('version.txt').text.trim()

apply plugin: "base"
apply plugin: "groovy"
apply plugin: "jacoco"
apply from: file('gradle/publishing.gradle')

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

repositories {
  jcenter()
}

dependencies {
  compile "org.slf4j:slf4j-api:${slf4jVersion}"
  compile "io.ratpack:ratpack-core:${ratpackVersion}"
  compile "io.ratpack:ratpack-guice:${ratpackVersion}"

  testCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
  testCompile "io.ratpack:ratpack-groovy-test:${ratpackVersion}"
  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
  testRuntime 'cglib:cglib-nodep:2.2.2'
  testRuntime 'org.objenesis:objenesis:1.2'
}

